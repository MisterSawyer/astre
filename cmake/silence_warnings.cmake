function(silence_warnings)
    set(options)
    set(oneValueArgs)
    set(multiValueArgs TARGETS)
    cmake_parse_arguments(silence_warnings "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN} )

    foreach(tgt ${silence_warnings_TARGETS})
        get_target_property(alias_actual ${tgt} ALIASED_TARGET)
        if(alias_actual)
            set(real_target ${alias_actual})
        else()
            set(real_target ${tgt})
        endif()

        if(NOT TARGET ${real_target})
            message(FATAL_ERROR "Target ${real_target} not found, cannot silence warnings.")
        endif()

        get_target_property(type ${real_target} TYPE)

        if(MSVC)
            if("${type}" STREQUAL "INTERFACE_LIBRARY")
                target_compile_options(${real_target} INTERFACE /W0 /WX- /analyze-)
            else()
                target_compile_options(${real_target} PRIVATE /W0 /WX- /analyze-)
            endif()
        elseif(CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
            if("${type}" STREQUAL "INTERFACE_LIBRARY")
                target_compile_options(${real_target} INTERFACE -w)
            else()
                target_compile_options(${real_target} PRIVATE -w)
            endif()
        endif()
    endforeach()
endfunction()